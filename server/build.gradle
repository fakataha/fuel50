plugins {
    id "org.unbroken-dome.test-sets" version "4.0.0"
    id 'com.avast.gradle.docker-compose' version '0.14.0'
    id 'org.springframework.boot' version '2.4.2'
    id 'com.diffplug.spotless' version '5.9.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group 'com.fuel50.homework'
version '1.1'

repositories {
    mavenCentral()
}

testSets {
    componentTest
    integrationTest { extendsFrom componentTest}
    smokeTest { extendsFrom integrationTest }
}

ext {
    junitVersion = '5.7.1'
    assertJVersion = '3.11.1'
    mySqlVersion = '8.0.23'
    h2Version = '1.4.200'
    restAssuredVersion = '4.2.0'
    mockitoVersion = '3.11.1'
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "mysql:mysql-connector-java:${mySqlVersion}"

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation "com.h2database:h2:${h2Version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.assertj:assertj-core:${assertJVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    componentTestImplementation "com.h2database:h2:${h2Version}"
    componentTestImplementation "org.springframework.boot:spring-boot-starter-test"
    componentTestImplementation "io.rest-assured:rest-assured:${restAssuredVersion}"
    componentTestImplementation "io.rest-assured:json-path:${restAssuredVersion}"
    componentTestImplementation "io.rest-assured:xml-path:${restAssuredVersion}"
    componentTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    componentTestImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

spotless {
    java {
        removeUnusedImports()
        googleJavaFormat()
    }
}
build.dependsOn spotlessApply

test {
    useJUnitPlatform()
}

task copyDocker(type: Copy) {
    from "${ rootProject.projectDir}/docker"
    into "${rootProject.buildDir}"
}

dockerCompose {
    useComposeFiles = ["${rootProject.buildDir}/docker-compose.yml"]
    projectName = project.name
}
composeUp.dependsOn copyDocker

integrationTest.dependsOn composeUp
integrationTest.finalizedBy composeDown